#!/bin/zsh
# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$(dirname "$(dirname "$(readlink ~/.zshrc)"../)")"

# Load custom settings and secrets
test -f $HOME/.localrc && source $HOME/.localrc

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load exports
for file in ${(M)config_files:#*/exports.zsh}; do
	source "$file"
done

# remove duplicates from PATH
typeset -U PATH

# load antibody plugins
source ~/.zsh_plugins.sh

# load init files
for file in ${(M)config_files:#*/init.zsh}; do
	source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
	compinit
done
compinit -C

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
	source "$file"
done

unset config_files
