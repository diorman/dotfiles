#!/bin/zsh

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$(dirname "$(dirname "$(readlink ~/.zshrc)"../)")"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load exports
for file in ${(M)config_files:#*/exports.zsh}; do
	source "$file"
done

# Load custom settings and secrets
test -f $HOME/.zshrc.local && source $HOME/.zshrc.local

# remove duplicates from PATH
typeset -U PATH

# load antibody plugins
source ~/.zsh_plugins.sh

# load init files
for file in ${(M)config_files:#*/init.zsh}; do
	source "$file"
done

# completion; use cache if updated within 24h
autoload -Uz compinit
if [[ -n $HOME/.zcompdump(#qN.mh+24) ]]; then
  compinit -d $HOME/.zcompdump
else
  compinit -C
fi

unset config_files
